import org.gradle.internal.os.OperatingSystem

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.9.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url 'https://jitpack.io' }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "20"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "20"
    }

    java {
        sourceCompatibility = "20"
        targetCompatibility = "20"
    }

    project.ext.jomlVersion = "1.10.1"
    project.ext.lwjglVersion = "3.3.2"
    project.ext.coroutinesVersion = "1.7.3"
    project.ext.boilerVersion = "v2.1.0"
    project.ext.junitVersion = "5.10.0"
    switch (OperatingSystem.current()) {
        case OperatingSystem.LINUX:
            def osArch = System.getProperty("os.arch")
            project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
                    ? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
                    : "natives-linux"
            break
        case OperatingSystem.WINDOWS:
            def osArch = System.getProperty("os.arch")
            project.ext.lwjglNatives = osArch.contains("64")
                    ? "natives-windows${osArch.startsWith("aarch64") ? "-arm64" : ""}"
                    : "natives-windows-x86"
            break
        case OperatingSystem.MAC_OS:
            project.ext.lwjglNatives = System.getProperty("os.arch").startsWith("aarch64") ? "natives-macos-arm64" : "natives-macos"
            break
    }

    dependencies {
        testImplementation platform("org.junit:junit-bom:$junitVersion")
        testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")

        implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
        implementation "org.lwjgl:lwjgl"
        implementation "org.lwjgl:lwjgl-vulkan"
        implementation "org.lwjgl:lwjgl-vma"
        implementation "org.joml:joml:${jomlVersion}"
        implementation "com.github.knokko:vk-boiler:$boilerVersion"

        testRuntimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
        testRuntimeOnly "org.lwjgl:lwjgl-vma::$lwjglNatives"
        if (lwjglNatives == "natives-macos") testRuntimeOnly "org.lwjgl:lwjgl-vulkan::$lwjglNatives"
    }

    test {
        useJUnitPlatform()
    }
}

project(':graviks2d') {
    dependencies {
        implementation project(':graviks-interface')
        implementation "org.lwjgl:lwjgl-stb"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
        testRuntimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    }
}

project(':graviks-glfw') {
    dependencies {
        implementation project(':graviks-interface')
        implementation project(':graviks2d')
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

        implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

        implementation "org.lwjgl:lwjgl"
        implementation "org.lwjgl:lwjgl-glfw"
        implementation "org.lwjgl:lwjgl-stb"
        implementation "org.lwjgl:lwjgl-vma"
        implementation "org.lwjgl:lwjgl-vulkan"

        testRuntimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    }
}

project(':gruviks') {
    dependencies {
        implementation project(':graviks-interface')
    }
}

project(':gruviks-glfw') {
    dependencies {
        implementation project(':graviks-interface')
        implementation project(':graviks2d')
        implementation project(':gruviks')
        implementation project(':graviks-glfw')

        implementation "org.lwjgl:lwjgl-glfw"
        testRuntimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"

        testImplementation 'com.github.knokko:sample-profiler:v1.0.0'
        testImplementation 'com.github.knokko:memory-query:v1.0'
    }
}

project(':proc-model-core') {
    dependencies {
        implementation project(':graviks-interface')
    }
}

project(':proc-model-processor') {
    dependencies {
        implementation project(':proc-model-core')
        testImplementation project(':proc-model-test-case')
    }
}

project(':proc-model-compiler') {
    dependencies {
        implementation 'org.antlr:antlr4:4.13.0'
        implementation project(':proc-model-core')
        implementation project(':proc-model-processor')
        implementation project(':graviks-interface')

        testImplementation project(':proc-model-test-case')
    }
}

project(':proc-model-test-case') {
    dependencies {
        implementation project(':proc-model-core')
        implementation project(':graviks-interface')
    }
}

project(':proc-model-renderer') {
    dependencies {
        implementation project(':proc-model-core')
        implementation project(':proc-model-processor')
        implementation project(':graviks-interface')

        implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
        implementation "org.lwjgl:lwjgl"
        implementation "org.lwjgl:lwjgl-vulkan"
        implementation "org.lwjgl:lwjgl-vma"
        implementation "com.github.knokko:vk-boiler:$boilerVersion"
    }
}

project(':proc-model-2d') {
    dependencies {
        compileOnly project(':proc-model-core')
        compileOnly project(':proc-model-processor')
        compileOnly project(':proc-model-compiler')
        compileOnly project(':proc-model-renderer')
        compileOnly project(':graviks-interface')

        implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
        implementation "org.lwjgl:lwjgl"
        implementation "org.lwjgl:lwjgl-vulkan"
        implementation "org.lwjgl:lwjgl-vma"
        implementation "com.github.knokko:vk-boiler:$boilerVersion"
    }
}

project(':proc-model-playground') {
    dependencies {
        implementation project(':graviks2d')
        implementation project(':proc-model-core')
        implementation project(':proc-model-compiler')
        implementation project(':proc-model-processor')
        implementation project(':proc-model-renderer')
        implementation project(':proc-model-2d')

        implementation 'com.github.knokko:sample-profiler:v1.0.0'
        implementation "org.lwjgl:lwjgl-glfw"

        runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-vma::$lwjglNatives"
        if (lwjglNatives == "natives-macos") runtimeOnly "org.lwjgl:lwjgl-vulkan::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
    }
}

project(':proc-model-editor') {
    dependencies {
        implementation project(':graviks-interface')
        implementation project(':graviks2d')
        implementation project(':graviks-glfw')
        implementation project(':gruviks')
        implementation project(':gruviks-glfw')

        implementation project(':proc-model-core')
        implementation project(':proc-model-compiler')
        implementation project(':proc-model-processor')
        implementation project(':proc-model-renderer')

        implementation 'com.github.knokko:sample-profiler:v1.0.0'
        implementation "org.lwjgl:lwjgl-glfw"
        implementation 'org.antlr:antlr4:4.13.0'
    }
}

project(':proc-model-editor2d') {
    dependencies {
        implementation project(':graviks-interface')
        implementation project(':graviks2d')
        implementation project(':graviks-glfw')
        implementation project(':gruviks')
        implementation project(':gruviks-glfw')

        implementation project(':proc-model-core')
        implementation project(':proc-model-compiler')
        implementation project(':proc-model-processor')
        implementation project(':proc-model-renderer')
        implementation project(':proc-model-editor')
        implementation project(':proc-model-2d')

        implementation 'com.github.knokko:sample-profiler:v1.0.0'
        implementation "org.lwjgl:lwjgl-glfw"
        implementation 'org.antlr:antlr4:4.13.0'

        runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-vma::$lwjglNatives"
        if (lwjglNatives == "natives-macos") runtimeOnly "org.lwjgl:lwjgl-vulkan::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
        runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
    }
}
